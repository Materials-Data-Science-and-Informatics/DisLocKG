# Competency Questions (CQs)
We list a number of CQs that can be used to query the information related to the dislocation structure, the crystal structure information of a dislocation structure, and the dislocation simulation. 

## A. Crystal Structure and Dislocation Structure CQs. 
Suppose we want to focus on a dislocation structure with the 
following CQs could be used to extract the information regarding details of crystal and dislocation structures:

A1. [Which crystal structure has a cubic crystal system?](#a1)
A2. [What are the lattice parameters of length given a crystal structure?](#a2)
A3. [What are the lattice parameters of angle given a crystal structure?](#a3)
A4. [What are the slip systems of a given crystal structure?](#a4)
A5. [What are the slip planes of a given crystal structure?](#a5)
A6. [Given the space group of a crystal structure, what is the bravais lattice centering?](#a6)
A7. [Given the crystal structure, what are the corresponding space and point groups?](#a7)
A8. [In which slip plane is a dislocation move on?](#a9)
A9. [What is the Burgers vector of the dislocation?](#a10)
A10. [What is the Burgers vector magnitude of the dislocation?](#a11)
A11. [What is the slip direction given a slip plane (111) of the crystal structure?](#a12)
A12. [Given a 3-D vector instance (Burgers vector, vector position, etc.), what are the vector components?](#a13)
A13. [What is the vector magnitude given a 3-D vector instance ( Burgers vector, vector position, etc.)?]((#a14))
A14. [Given a Basis of a vector in 3-D space, what are the first axis basis vector?](#a15)
A15. [Given a Basis of a vector in 3-D space, what are the second axis basis vector?](#a16)
A16. [Given a 3-D vector instance Burgers vector, what is the unit?](#a17)

## B. Discrete Dislocation Dynamics (DDD) Simulation CQs

B1. [What is the material used in the DDD simulation?](#b1)
B2. [What are dislocation structures simulated with the MODELIB software? List also the software version and creator associated with those dislocation simulations](#b2)
B3. [What dislocation structures have a specimen shape of a cube with an edge length > 30nm?](#b3)
B4. [List all DDD simulations that do not activate the cross slip formation and junction formation](#b4)
B5. [List all DDD simulations that have initial density of dislocation < 1e17](#b5)
B6. [What are dislocation structures generated by the relaxation calculation? List also the **initial density** of a dislocation structure used for a relaxation calculation, simulation parameters: **cross-slip activation**, **junction formation activation**, and **external load activation**, and whether the dislocation structure is **relaxed**.](#b6)


#### A1
Which crystal structure that has a cubic crystal system?
```
PREFIX diso: <https://purls.helmholtz-metadaten.de/disos/diso#>
PREFIX cso: <https://purls.helmholtz-metadaten.de/disos/cso#> 
PREFIX cdo: <https://purls.helmholtz-metadaten.de/disos/cdo#>  

SELECT ?dislocationstructure ?crystalstructure ?crystal_system WHERE{
	?dislocationsimulation diso:hasInputDislocationStructure ?dislocationstructure.
	?dislocationstructure diso:relatesToCrystallineMaterial ?crystallinematerial .  
	?crystallinematerial cdo:hasCrystalStructure ?crystalstructure.
	?crystalstructure a cso:CrystalStructure ; 
	 	 cso:hasLattice ?lattice .
	?lattice cso:hasCrystalSystem ?crystal_system .
	?crystal_system a cso:Cubic .
}
```
[back](#a-crystal-structure-and-dislocation-structure-cqs)
### A2
What are the lattice parameters of length given a crystal structure?
```
PREFIX diso: <https://purls.helmholtz-metadaten.de/disos/diso#>
PREFIX cso: <https://purls.helmholtz-metadaten.de/disos/cso#> 
PREFIX cdo: <https://purls.helmholtz-metadaten.de/disos/cdo#>   

SELECT ?dislocationstructure ?crystalstructure ?length_a ?length_b ?length_c WHERE{
	?dislocationsimulation diso:hasInputDislocationStructure ?dislocationstructure.
	?dislocationstructure diso:relatesToCrystallineMaterial ?crystallinematerial .  
	?crystallinematerial cdo:hasCrystalStructure ?crystalstructure.
	?crystalstructure a cso:CrystalStructure ; 
	 	 cso:hasLattice ?lattice .
	?lattice cso:hasUnitCell ?unitcell .
	?unitcell cso:hasLatticeParameterLength ?latticeParameterLength.  
	?latticeParameterLength cso:latticeParameterLengthA ?length_a;
		cso:latticeParameterLengthB ?length_b;
		cso:latticeParameterLengthC ?length_c.
}
```
[back](#a-crystal-structure-and-dislocation-structure-cqs)
### A3
What are the lattice parameters of angle given a dislocation structure?
```
PREFIX diso: <https://purls.helmholtz-metadaten.de/disos/diso#>
PREFIX cso: <https://purls.helmholtz-metadaten.de/disos/cso#> 
PREFIX cdo: <https://purls.helmholtz-metadaten.de/disos/cdo#>  

SELECT ?dislocationstructure ?crystalstructure ?angle_alpha ?angle_beta ?angle_gamma WHERE{
	?dislocationsimulation diso:hasInputDislocationStructure ?dislocationstructure.
	?dislocationstructure diso:relatesToCrystallineMaterial ?crystallinematerial .  
	?crystallinematerial cdo:hasCrystalStructure ?crystalstructure.
	?crystalstructure a cso:CrystalStructure ; 
	 	 cso:hasLattice ?lattice .
	?lattice cso:hasUnitCell ?unitcell .
	?unitcell cso:hasLatticeParameterAngle ?latticeParameterAngle.
	?latticeParameterAngle cso:latticeParameterAngleAlpha ?angle_alpha;  
		cso:latticeParameterAngleBeta ?angle_beta;
		cso:latticeParameterAngleGamma ?angle_gamma.
}
```
[back](#a-crystal-structure-and-dislocation-structure-cqs)
### A4
What are the slip systems of a given dislocation structure?
```
PREFIX diso: <https://purls.helmholtz-metadaten.de/disos/diso#>
PREFIX cso: <https://purls.helmholtz-metadaten.de/disos/cso#> 
PREFIX cdo: <https://purls.helmholtz-metadaten.de/disos/cdo#>   

SELECT ?dislocationstructure ?crystalstructure ?slip_system ?slip_plane_normal_val ?slip_direction_val WHERE{
	?dislocationsimulation diso:hasInputDislocationStructure ?dislocationstructure.
	?dislocationstructure diso:relatesToCrystallineMaterial ?crystallinematerial .  
	?crystallinematerial cdo:hasCrystalStructure ?crystalstructure.
	?crystalstructure diso:hasSlipSystem ?slip_system FILTER CONTAINS(STR(?slip_system), 'input'). 
	?slip_system diso:hasSlipPlaneNormal ?slip_plane_normal ; 
		diso:hasSlipDirection ?slip_direction.
	?slip_plane_normal diso:directionMillerIndice ?slip_plane_normal_val. 
	?slip_direction diso:directionMillerIndice ?slip_direction_val.
}
```
[back](#a-crystal-structure-and-dislocation-structure-cqs)
### A5
What are the slip planes of a given dislocation structure?
```
PREFIX diso: <https://purls.helmholtz-metadaten.de/disos/diso#>
PREFIX cso: <https://purls.helmholtz-metadaten.de/disos/cso#> 
PREFIX cdo: <https://purls.helmholtz-metadaten.de/disos/cdo#>   

SELECT ?dislocationstructure ?slip_plane ?slip_plane_miller_indice WHERE{
	?dislocationsimulation diso:hasInputDislocationStructure ?dislocationstructure.
	?dislocationstructure diso:relatesToCrystallineMaterial ?crystallinematerial .  
	?crystallinematerial cdo:hasCrystalStructure ?crystalstructure.
	?crystalstructure diso:hasSlipPlane ?slip_plane FILTER CONTAINS(STR(?slip_plane), 'input'). 
	?slip_plane diso:planeMillerIndice ?slip_plane_miller_indice .
}
```
[back](#a-crystal-structure-and-dislocation-structure-cqs)
### A6
Given the space group of a crystal structure, what is the bravais lattice centering?
```
PREFIX diso: <https://purls.helmholtz-metadaten.de/disos/diso#>
PREFIX cso: <https://purls.helmholtz-metadaten.de/disos/cso#> 
PREFIX cdo: <https://purls.helmholtz-metadaten.de/disos/cdo#>   

SELECT ?dislocationstructure ?crystalstructure ?centering WHERE{
	?dislocationsimulation diso:hasInputDislocationStructure ?dislocationstructure.
	?dislocationstructure diso:relatesToCrystallineMaterial ?crystallinematerial .  
	?crystallinematerial cdo:hasCrystalStructure ?crystalstructure.
    ?crystalstructure cso:hasLattice ?bravais_lattice.
	?bravais_lattice cso:centering ?centering . 
}
```
[back](#a-crystal-structure-and-dislocation-structure-cqs)
### A7
Given the dislocation structure, what are the corresponding space and point groups?
```
PREFIX diso: <https://purls.helmholtz-metadaten.de/disos/diso#>
PREFIX cso: <https://purls.helmholtz-metadaten.de/disos/cso#> 
PREFIX cdo: <https://purls.helmholtz-metadaten.de/disos/cdo#>   
PREFIX mdo: <https://w3id.org/mdo/structure/> 

SELECT  ?dislocationstructure ?crystalstructure ?space_group ?point_group WHERE{
	?dislocationsimulation diso:hasInputDislocationStructure ?dislocationstructure.
	?dislocationstructure diso:relatesToCrystallineMaterial ?crystallinematerial .  
	?crystallinematerial cdo:hasCrystalStructure ?crystalstructure.
	?crystalstructure mdo:hasSpaceGroup ?sg.
	?sg mdo:hasPointGroup ?pg ;
	 	mdo:SpaceGroupSymbol ?space_group . 
	?pg mdo:PointGroupHMName ?point_group .
}
```

[back](#a-crystal-structure-and-dislocation-structure-cqs)
### A8
In which slip plane is a dislocation move on?
```
PREFIX diso: <https://purls.helmholtz-metadaten.de/disos/diso#>
PREFIX cso: <https://purls.helmholtz-metadaten.de/disos/cso#> 
PREFIX cdo: <https://purls.helmholtz-metadaten.de/disos/cdo#>  

SELECT  ?dislocationstructure ?dislocation ?slip_plane ?slip_plane_miller_indice
WHERE{
    ?dislocationsimulation diso:hasInputDislocationStructure ?dislocationstructure.
    ?dislocationstructure cdo:hasCrystallographicDefect ?dislocation .  
	?dislocation diso:movesOn ?slip_plane . 
	?slip_plane diso:planeMillerIndice ?slip_plane_miller_indice . 
}
```
[back](#a-crystal-structure-and-dislocation-structure-cqs)
### A9
What is the Burgers vector of the dislocation?
```
PREFIX diso: <https://purls.helmholtz-metadaten.de/disos/diso#>
PREFIX cso: <https://purls.helmholtz-metadaten.de/disos/cso#> 
PREFIX cdo: <https://purls.helmholtz-metadaten.de/disos/cdo#>  


SELECT ?dislocationstructure ?dislocation ?firstAxisComponent ?secondAxisComponent ?thirdAxisComponent  WHERE{
	?dislocationsimulation diso:hasInputDislocationStructure ?dislocationstructure.
    ?dislocationstructure cdo:hasCrystallographicDefect ?dislocation .  
	?dislocation diso:hasBurgersVector ?Burgers_vector.
	?Burgers_vector	cso:hasVectorComponent ?VectorComponentOfBasis .
	?VectorComponentOfBasis cso:firstAxisComponent ?firstAxisComponent;
		cso:secondAxisComponent ?secondAxisComponent;
		cso:thirdAxisComponent ?thirdAxisComponent .
}
```
[back](#a-crystal-structure-and-dislocation-structure-cqs)
### A10
What is the Burgers vector magnitude of the dislocation?
```
PREFIX diso: <https://purls.helmholtz-metadaten.de/disos/diso#>
PREFIX cso: <https://purls.helmholtz-metadaten.de/disos/cso#> 
PREFIX cdo: <https://purls.helmholtz-metadaten.de/disos/cdo#>  

SELECT ?dislocation ?Burgers_vector_magnitude  WHERE{
	?dislocationsimulation diso:hasInputDislocationStructure ?dislocationstructure.
    ?dislocationstructure cdo:hasCrystallographicDefect ?dislocation . 
	?dislocation diso:hasBurgersVector ?Burgers_vector.
	?Burgers_vector cso:vectorMagnitude ?Burgers_vector_magnitude .
}
```
[back](#a-crystal-structure-and-dislocation-structure-cqs)
### A11
What is the slip direction given a slip plane (111) of the crystal structure?
```
PREFIX diso: <https://purls.helmholtz-metadaten.de/disos/diso#>
PREFIX cso: <https://purls.helmholtz-metadaten.de/disos/cso#> 
PREFIX cdo: <https://purls.helmholtz-metadaten.de/disos/cdo#>  
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT  ?dislocationstructure ?slip_plane ?slip_direction_val WHERE{
    	?dislocationsimulation diso:hasInputDislocationStructure ?dislocationstructure.
	?dislocationstructure diso:relatesToCrystallineMaterial ?crystallinematerial .  
	?crystallinematerial cdo:hasCrystalStructure ?crystalstructure.
		?crystalstructure a cso:CrystalStructure ;
     		diso:hasSlipPlane ?slip_plane FILTER CONTAINS(STR(?slip_plane), 'input').
        	?slip_plane diso:planeMillerIndice "(1 1 1)"^^xsd:string;
     		diso:hasSlipDirection ?slip_direction. 
	?slip_direction diso:directionMillerIndice ?slip_direction_val.
}
```
[back](#a-crystal-structure-and-dislocation-structure-cqs)
### A12
Given a 3-D vector instance (Burgers vector, vector position, etc.), what are the vector components?
```
PREFIX diso: <https://purls.helmholtz-metadaten.de/disos/diso#>
PREFIX cso: <https://purls.helmholtz-metadaten.de/disos/cso#> 
PREFIX cdo: <https://purls.helmholtz-metadaten.de/disos/cdo#>   

SELECT ?Burgers_vector ?firstAxisComponent ?secondAxisComponent ?thirdAxisComponent WHERE{
	?Burgers_vector a diso:BurgersVector;
		cso:hasVectorComponent ?VectorComponentOfBasis .
	?VectorComponentOfBasis cso:firstAxisComponent ?firstAxisComponent;
		cso:secondAxisComponent ?secondAxisComponent;
		cso:thirdAxisComponent ?thirdAxisComponent .
}
```
[back](#a-crystal-structure-and-dislocation-structure-cqs)
### A13
What is the vector magnitude given a 3-D vector instance (Burgers vector, vector position, etc.)?
```
PREFIX diso: <https://purls.helmholtz-metadaten.de/disos/diso#>
PREFIX cso: <https://purls.helmholtz-metadaten.de/disos/cso#> 
PREFIX cdo: <https://purls.helmholtz-metadaten.de/disos/cdo#>   

SELECT ?Burgers_vector ?vectorMagnitude WHERE{
	?Burgers_vector a diso:BurgersVector;
		cso:vectorMagnitude ?vectorMagnitude .
}
```
[back](#a-crystal-structure-and-dislocation-structure-cqs)
### A14
Given a Basis of a vector in 3-D space, what are the first axis basis vector?
```
PREFIX diso: <https://purls.helmholtz-metadaten.de/disos/diso#>
PREFIX cso: <https://purls.helmholtz-metadaten.de/disos/cso#> 
PREFIX cdo: <https://purls.helmholtz-metadaten.de/disos/cdo#>
PREFIX mdo: <https://w3id.org/mdo/structure/>    

SELECT ?Burgers_vector ?first_axis_basis_e_x ?first_axis_basis_e_y ?first_axis_basis_e_z WHERE{
	?Burgers_vector a diso:BurgersVector;
		cso:hasVectorComponent ?VectorComponents . 
	?VectorComponents cso:hasBasis ?Basis .
	?Basis cso:hasFirstAxisVector ?CoordinateVector .
	?CoordinateVector mdo:X_axisCoordinate ?first_axis_basis_e_x;
		mdo:Y_axisCoordinate ?first_axis_basis_e_y;
		mdo:Z_axisCoordinate ?first_axis_basis_e_z . 
}
```
[back](#a-crystal-structure-and-dislocation-structure-cqs)
### A15
Given a Basis of a vector in 3-D space, what are the second axis basis vector?
```
PREFIX diso: <https://purls.helmholtz-metadaten.de/disos/diso#>
PREFIX cso: <https://purls.helmholtz-metadaten.de/disos/cso#> 
PREFIX cdo: <https://purls.helmholtz-metadaten.de/disos/cdo#>
PREFIX mdo: <https://w3id.org/mdo/structure/>    

SELECT ?Burgers_vector ?second_axis_basis_e_x ?second_axis_basis_e_y ?second_axis_basis_e_z WHERE{
	?Burgers_vector a diso:BurgersVector;
		cso:hasVectorComponent ?VectorComponents . 
	?VectorComponents cso:hasBasis ?Basis .
	?Basis cso:hasSecondAxisVector ?CoordinateVector .
	?CoordinateVector mdo:X_axisCoordinate ?second_axis_basis_e_x;
		mdo:Y_axisCoordinate ?second_axis_basis_e_y;
		mdo:Z_axisCoordinate ?second_axis_basis_e_z . 
}
```
[back](#a-crystal-structure-and-dislocation-structure-cqs)
### A16
Given a 3-D vector instance Burgers vector, what is the unit?
```
PREFIX diso: <https://purls.helmholtz-metadaten.de/disos/diso#>
PREFIX cso: <https://purls.helmholtz-metadaten.de/disos/cso#> 
PREFIX cdo: <https://purls.helmholtz-metadaten.de/disos/cdo#>   
PREFIX qudt: <http://qudt.org/schema/qudt/>

SELECT ?Burgers_vector ?unit ?quantity_kind WHERE{
	?Burgers_vector a diso:BurgersVector;
		cso:hasVectorComponent ?VectorComponents .
	?VectorComponents qudt:hasQuantityKind ?quantity_kind ;
		qudt:quantityValue ?qv.
	?qv qudt:unit ?unit.
}
```
[back](#a-crystal-structure-and-dislocation-structure-cqs)

### B1
What is the material used in the DDD simulation?
```
PREFIX diso: <https://purls.helmholtz-metadaten.de/disos/diso#>
PREFIX mdo_struc: <https://w3id.org/mdo/structure/>

SELECT ?dislocationsimulation ?chemicalformula WHERE{
?dislocationsimulation diso:hasInputDislocationStructure ?dislocationstructure.
  ?dislocationstructure mdo_struc:hasComposition ?composition.
  ?composition mdo_struc:DescriptiveFormula ?chemicalformula.
}
```

[back](#b-discrete-dislocation-dynamics-ddd-simulation-cqs)
### B2
What are dislocation structures simulated with the MODELIB software? List also the software version and creator associated with those dislocation simulations
```
PREFIX diso: <https://purls.helmholtz-metadaten.de/disos/diso#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX mdo_prov: <https://w3id.org/mdo/provenance/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX mwo: <http://purls.helmholtz-metadaten.de/mwo/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?dislocationstructure ?software_name ?soft_version ?data_creator_first_name ?data_creator_last_name ?data_creator_orcid WHERE{
    ?dislocationsimulation diso:hasOutputDislocationStructure ?dislocationstructure.
    ?dislocationsimulation prov:wasAssociatedWith ?data_creator. 
    ?data_creator a prov:Person ; 
    	foaf:firstName ?data_creator_first_name; 
    	foaf:family_name ?data_creator_last_name;
    	mwo:hasORCID ?data_creator_orcid.
    ?dislocationsimulation prov:wasAssociatedWith ?software.
    ?software a prov:SoftwareAgent;
        mdo_prov:SoftwareName ?software_name;
        diso:softwareVersion ?soft_version.
FILTER (?software_name = "MODELIB"^^xsd:string)
}
```
[back](#b-discrete-dislocation-dynamics-ddd-simulation-cqs)
### B3
List dislocation structures that have a specimen shape of a cube with an edge length > 30nm?
```
PREFIX diso: <https://purls.helmholtz-metadaten.de/disos/diso#>
PREFIX qudt: <http://qudt.org/schema/qudt/>

SELECT ?dislocationsimulation ?dislocationstructure  ?edge_length ?unit WHERE{
    ?dislocationsimulation diso:hasInputDislocationStructure ?dislocationstructure. 
    ?dislocationstructure diso:hasShape ?geometry_shape. 
    ?geometry_shape a diso:Cube.
    ?geometry_shape diso:hasLength ?length. 
    ?length qudt:quantityValue ?l_qv. 
    ?l_qv qudt:unit ?unit ; 
    	qudt:numericalValue ?edge_length.
FILTER (?edge_length > 30.0)
}
```
[back](#b-discrete-dislocation-dynamics-ddd-simulation-cqs)
### B4
List all DDD simulations that do not activate the cross slip formation and junction formation.
```
PREFIX diso: <https://purls.helmholtz-metadaten.de/disos/diso#>
PREFIX qudt: <http://qudt.org/schema/qudt/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?dislocationsimulation ?cross_slip_activ ?junction_form_activ WHERE
{
    ?dislocationsimulation diso:hasSimulationParameter ?cross_slip, ?junction_form. 
    ?cross_slip a diso:CrossSlip; 
        diso:parameterActivation ?cross_slip_activ FILTER(?cross_slip_activ = "false"^^xsd:boolean).
    ?junction_form a diso:JunctionFormation; 
        diso:parameterActivation ?junction_form_activ FILTER(?junction_form_activ = "false"^^xsd:boolean).
}
```
[back](#b-discrete-dislocation-dynamics-ddd-simulation-cqs)
### B5
List all DDD simulations that have initial density of dislocation < 1e17
```
PREFIX diso: <https://purls.helmholtz-metadaten.de/disos/diso#>
PREFIX qudt: <http://qudt.org/schema/qudt/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX mdo_core: <https://w3id.org/mdo/core/>
 
SELECT ?ddd_simulation ?initial_density ?unit WHERE{
    ?ddd_simulation diso:hasInputDislocationStructure ?input_dislocationstructure.
    ?initial_density_qu mdo_core:relatesToStructure ?input_dislocationstructure ; 
        qudt:quantityValue ?density_qv.
    ?density_qv qudt:numericalValue ?initial_density;
        qudt:unit ?unit.
    FILTER(?initial_density < "1e17"^^xsd:double)
}
```
[back](#b-discrete-dislocation-dynamics-ddd-simulation-cqs)
### B6
What are dislocation structures generated by the relaxation calculation? List also the **initial density** of a dislocation structure used for a relaxation calculation, simulation parameters: **cross-slip activation**, **junction formation activation**, and **external load activation**, and whether the dislocation structure is **relaxed**.
```
PREFIX diso: <https://purls.helmholtz-metadaten.de/disos/diso#>
PREFIX qudt: <http://qudt.org/schema/qudt/>
PREFIX mdo_core: <https://w3id.org/mdo/core/>

SELECT ?output_dislocationstructure ?initial_density ?unit ?cross_slip_activ ?junction_form_activ ?load_activ ?is_relaxed  WHERE{
    ?dislocationsimulation diso:simulationDescription ?relax FILTER CONTAINS(?relax, "relax").
    ?dislocationsimulation diso:hasOutputDislocationStructure ?output_dislocationstructure. 
    ?dislocationsimulation diso:hasInputDislocationStructure ?input_dislocationstructure. 
    ?initial_density_qu mdo_core:relatesToStructure ?input_dislocationstructure ; 
        qudt:quantityValue ?density_qv.
    ?density_qv qudt:numericalValue ?initial_density;
        qudt:unit ?unit.
     ?output_dislocationstructure diso:isRelaxed ?is_relaxed.
OPTIONAL{
     ?dislocationsimulation diso:hasSimulationParameter ?cross_slip, ?junction_form, ?load. 
    ?cross_slip a diso:CrossSlip; 
        diso:parameterActivation ?cross_slip_activ.
    ?junction_form a diso:JunctionFormation; 
        diso:parameterActivation ?junction_form_activ. 
    ?load a diso:ExternalLoadParameter; 
        diso:parameterActivation ?load_activ.}
}
```
[back](#b-discrete-dislocation-dynamics-ddd-simulation-cqs)

